plugins {
    id 'java'
    id 'war'
    id 'idea'
    id "com.moowork.node" version "1.3.1"
}

group 'com.ikatas'
version '1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    jettyVersion = "9.4.30.v20200611"
}

repositories {
    mavenLocal()
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
}

dependencies {
    implementation 'javax.websocket:javax.websocket-api:1.1'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'net.sourceforge.htmlunit:htmlunit:2.42.0'
    testImplementation "org.eclipse.jetty:jetty-webapp:$jettyVersion"
    testImplementation "org.eclipse.jetty:jetty-annotations:$jettyVersion"
    testImplementation "org.eclipse.jetty.websocket:javax-websocket-server-impl:$jettyVersion"
}

war {
    archiveFileName.set("${project.name}.${archiveExtension.get()}")
}

idea {
    module {
        excludeDirs += [file('.idea'), file('.gradle')]
        downloadSources = true
        downloadJavadoc = true
    }
}

check.dependsOn(npm_test)
war.dependsOn(npm_run_build)
test.dependsOn(npm_run_build)

tasks.withType(NpmTask) {
    environment = [production: true, webSocketServerURL: 'ws://localhost:8080/chat']
}

clean {
    delete(fileTree(webAppDirName))
}